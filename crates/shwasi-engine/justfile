log := "warn"
bt := "0"
export RUST_BACKTRACE := bt
export RUST_LOG := log
set fallback

default: parent

parent:
  @just buildtest
  @just build
  @just check
  @just test
  @just fmt

mktest NAME KIND="good":
  #!/bin/bash
  set -euo pipefail
  touch ./tests/inputs/dumbwasm/{{NAME}}.dumbwasm
  cd ./tests/inputs
  touch {{NAME}}.wasm
  echo "pub const {{uppercase(NAME)}}: &[u8] = include_bytes!(\"./inputs/{{NAME}}.wasm\");" >> ../inputs.rs
  echo created test, {{NAME}}.dumbwasm!
  if [[ {{KIND}} == "good" ]]; then
    printf "\n\n#[test]\nfn {{NAME}}() {\n    let module = Parser::new({{uppercase(NAME)}}).read_module().unwrap();\n    assert_snapshot!(pretty_fmt(&module));\n}" >> ../tests.rs
  else
    printf "\n\n#[test]\nfn {{NAME}}() {\n    let result = Parser::new({{uppercase(NAME)}}).read_module();\n    let err = result.unwrap_err();\n    assert_display_snapshot!(err.root_cause(), @\"\");\n}" >> ../tests.rs
  fi
  $EDITOR ./dumbwasm/{{NAME}}.dumbwasm

crash NAME:
  #!/bin/bash
  set -euo pipefail
  mv crash.wasm ./tests/inputs/{{NAME}}.wasm
  cd ./tests/inputs
  echo "pub const {{uppercase(NAME)}}: &[u8] = include_bytes!(\"./inputs/{{NAME}}.wasm\");" >> ../inputs.rs
  printf "\n\n#[test]\nfn {{NAME}}() {\n    let module = Parser::new({{uppercase(NAME)}}).read_module().unwrap();\n    assert_snapshot!(pretty_fmt(&module));\n}" >> ../tests.rs
  echo "created crash test, {{NAME}}.wasm!"

buildtest:
  cd tests/inputs && ls dumbwasm | xargs -I{} dumbwasm ./dumbwasm/{}
  @echo built test files!

fuzz *ARGS="parse":
  cargo fuzz run {{ARGS}}
